pragma Singleton

import QtQuick
import Quickshell
import Quickshell.Io
import Caelestia.Config

Singleton {
    id: root

    // This fork does not use the mail widget. Keep the backend inert even if a
    // default bar entry or another component references the singleton.
    readonly property bool enabled: false
    property var unreadEmails: []

    property int refCount

    reloadableId: "mailText"

    Timer {
        running: root.enabled && root.refCount > 0
        interval: 5000
        repeat: true
        triggeredOnStart: true
        onTriggered: {
            getUnreadEmails.running = true;
        }
    }

    Process {
        id: getUnreadEmails

        running: root.enabled
        command: GlobalConfig.bar.mail.fetchCommand
        // qmllint disable incompatible-type
        environment: ({
                LANG: "C.UTF-8",
                LC_ALL: "C.UTF-8"
            })
        // qmllint enable incompatible-type
        stdout: StdioCollector {
            onStreamFinished: {
                try {
                    const json = JSON.parse(text);
                    // stripEmoji to get rid  of QT warnings:
                    // WARN: render glyph failed err=9e face=0x7f989de17600, glyph=891
                    // WARN: QFontEngine: Glyph rendered in unknown pixel_mode=0
                    const stripEmoji = str => str.replace(/\p{Emoji_Presentation}/gu, '').trim();
                    const unreadEmails = json.filter(m => m && m.authors && m.subject).map(m => ({
                                author: m.authors,
                                subject: stripEmoji(m.subject)
                            }));
                    root.unreadEmails = unreadEmails;
                } catch (e) {
                    console.error("Failed to parse mail output:", e.message);
                    root.unreadEmails = [];
                }
            }
        }
    }
}
